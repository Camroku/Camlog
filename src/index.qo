#!/usr/local/bin/qo
include lib.wutils;
include lib.qomar;
include lib.sort;
include qcf;
include qo;

qcf.new("config.qcf", &config);

config.author = config.get("author");
author = config.author;

route = deleteAfter(qo.env("REQUEST_URI"), '?');

header = format(read("views/header.html"));

func deleteBlank(list)
{
    outlist = [];
    foreach &element in list
    {
        if (element != "") {
            outlist += [element];
        };
    };
    return outlist;
};

if (route == "/")
{
    pagelist = [];
    datelist = [];
    foreach &page in filelist("./pages")
    {
        pagecontent = read("pages/" + page);
        splitted = split(pagecontent, %"\n-----\n");
        metadata = split(splitted[0], %"\n");
        foreach &data in metadata
        {
            datasplitted = split(data, " ");
            datasplitted = deleteBlank(datasplitted);
            if (datasplitted[0] == "title")
            {
                pagename = join(datasplitted[1:], " ");
            }
            else if (datasplitted[0] == "date")
            {
                date = toInt(datasplitted[1]);
            };
        };
        pagelist += [[page, pagename]];
        datelist += [date];
    };
    ret = sort.sortbyvalr(pagelist, datelist);
    pages = ret[0];
    datelist = ret[1];
    remove(ret);
    pagelist = "";
    i = 0;
    foreach &page in pages
    {
        pagelink = replace(page[0], ".qm", "");
        pagename = page[1];
        pagedate = formatdate(datelist[i], '%Y-%m-%d %H:%M:%S');
        pagelist += %"<h3><a href=\"/p/$pagelink\">$pagename</a><span style=\"font-size: 10px;\"> $pagedate</span></h3>";
        i += 1;
    };
    content = format(read("views/index.html"));
    wutils.htmlPage(content);
}
else
{
    splitted = split(route, "/");
    splitted = deleteBlank(splitted);
    if (splitted[0] == "p") {
        if (len(splitted) == 1) {
            wutils.redirect("/");
        } else {
            pagecontent = read("pages/" + splitted[1] + ".qm");
            if (pagecontent == None) {
                wutils.error("404 Not Found");
            } else {
                splitted = split(pagecontent, %"\n-----\n");
                pagecontent = qomar.compile.html(join(splitted[1:], %"\n-----\n"));
                metadata = split(splitted[0], %"\n");
                foreach &data in metadata
                {
                    datasplitted = split(data, " ");
                    datasplitted = deleteBlank(datasplitted);
                    if (datasplitted[0] == "title")
                    {
                        page = join(datasplitted[1:], " ");
                    }
                    else if (datasplitted[0] == "date")
                    {
                        date = formatdate(toInt(datasplitted[1]), '%Y-%m-%d %H:%M:%S');
                    }
                    else if (datasplitted[0] == "author")
                    {
                        author = join(datasplitted[1:], " ");
                    };
                };
                content = format(read("views/page.html"));
                wutils.htmlPage(content);
            };
        };
    } else {
        wutils.error("404 Not Found");
    };
};
